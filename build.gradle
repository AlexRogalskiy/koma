buildscript {
    ext.kotlin_version = '1.2.20'
    ext.kotlin_native_version = '0.6'

    repositories {
        mavenCentral()
        maven { url  "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:$kotlin_native_version"
    }
}

// TODO: Deal with codegen, and that intellij wont allow `common` to be imported in three projs

project(':matrix-common') {
    apply plugin: 'kotlin-platform-common'

    sourceSets.main.kotlin.srcDirs = ['src', 'srcgen']

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
    }
}


project(":matrix-ejml") {
    apply plugin: 'kotlin-platform-jvm'

    sourceSets.main.kotlin.srcDirs = ['src', '../common/src']


    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.ejml:all:0.27"

        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        expectedBy project(":matrix-common")
        testCompile "junit:junit:4.12"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    }
}
project(":matrix-jblas") {
    apply plugin: 'kotlin-platform-jvm'

    sourceSets.main.kotlin.srcDirs = ['src', '../common/src']


    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jblas:jblas:1.2.3"

        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        expectedBy project(":matrix-common")
        testCompile "junit:junit:4.12"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    }
}

project(":matrix-mtj") {
    apply plugin: 'kotlin-platform-jvm'

    sourceSets.main.kotlin.srcDirs = ['src', '../common/src']

    repositories {
        mavenCentral()
    }

    dependencies {
        compile("com.googlecode.matrix-toolkits-java:mtj:1.0.4") {
            // Fix issue with pom-only artifacts in MTJ
            exclude module: "com.github.fommil.netlib:all:pom:1.1.2"
        }

        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        expectedBy project(":matrix-common")
        testCompile "junit:junit:4.12"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    }
}

project(":matrix-js") {
    apply plugin: 'kotlin-platform-js'

    sourceSets.main.kotlin.srcDirs = ['src']

    repositories {
        mavenCentral()
    }

    compileKotlin2Js.kotlinOptions.outputFile = "node_modules/koma-core.js"
    compileKotlin2Js.kotlinOptions.moduleKind = "commonjs"
    
    dependencies {

        compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
        expectedBy project(":matrix-common")
        testCompile "junit:junit:4.12"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    }
}

project(":matrix-cblas") {
    apply plugin: 'konan'

    konanArtifacts {
        interop('cblas') {
            // TODO: `headers` here, but search the include path for a blas header,
            // removing the hardcoded one in the defFile
            defFile('cblas.def')
        }
        interop('lapacke') {
            defFile('lapacke.def')
        }
        dynamic('libkoma') {
            enableMultiplatform true
            srcFiles fileTree('src')
            linkerOpts "-S"
            enableOptimizations(true)
            libraries {
                artifact 'cblas'
                artifact 'lapacke'
            }
        }
        library('koma') {
            enableMultiplatform true
            srcFiles fileTree('src')
            linkerOpts "-S"
            enableOptimizations(true)
            libraries {
                artifact 'cblas'
                artifact 'lapacke'
            }
        }
        program('komaExample') {

            libraries {
                artifact 'koma'
                srcFiles '../examples/native/main.kt'
            }
        }
    }
    dependencies {
        expectedBy project(":matrix-common")
    }
}

task codegen {
    doLast {
        ['Double', 'Float', 'Int', 'Long'].forEach { dtype ->
            copy {
                from 'matrix-common/templates'
                into 'matrix-common/srcgen/koma.matrix.default'
                include 'DefaultXMatrix.kt'
                rename { "Default${dtype}Matrix.kt" }

                expand(div: getDiv(dtype),
                        epow: getEPow(dtype),
                        dtype: dtype)
            }
            copy {
                from 'matrix-common/templates'
                into 'matrix-common/srcgen/koma.matrix.default'
                include 'DefaultXMatrixFactory.kt'
                rename { "Default${dtype}MatrixFactory.kt" }
                expand(dtype: dtype)
            }
            copy {
                from 'matrix-common/templates'
                into 'matrix-common/srcgen/koma.ndarray.default'
                include 'DefaultXNDArray.kt'
                rename { "Default${dtype}NDArray.kt"}
                expand(dtype: dtype)
            }
        }
    }

}
def getDiv(dtype) {
    if (dtype != 'Int')
        return """
    override fun div(other: ${dtype}): Matrix<${dtype}>
            = this.mapIndexed { _, _, ele -> ele/other}
"""
    else
        return ""
}
def getEPow(dtype) {
    if (dtype != 'Int')
        return """
    override fun epow(other: ${dtype}): Matrix<${dtype}> 
            = this.mapIndexed { _, _, ele -> pow(ele.toDouble(), other.toDouble()).to${dtype}() }
"""
    else
        return ""

}